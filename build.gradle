plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.6'
	id 'io.spring.dependency-management' version '1.1.7'

	id 'com.github.spotbugs' version '6.0.20'
	id "org.owasp.dependencycheck" version "12.1.8"
}

group = 'sec.lab1'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'

	implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
	runtimeOnly  'io.jsonwebtoken:jjwt-impl:0.12.5'
	runtimeOnly  'io.jsonwebtoken:jjwt-jackson:0.12.5'

	implementation 'org.owasp.encoder:encoder:1.2.3'

	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testRuntimeOnly 'com.h2database:h2'

	constraints {
    	implementation("org.apache.commons:commons-lang3:3.19.0")
  	}
}

tasks.named('test') {
	useJUnitPlatform()

	systemProperty "spring.profiles.active", System.getProperty("spring.profiles.active", "test")
}

ext['commons-lang3.version'] = '3.19.0'

configurations.configureEach {
  resolutionStrategy.eachDependency { details ->
    if (details.requested.group == 'org.apache.commons' &&
        details.requested.name == 'commons-lang3') {
      details.useVersion '3.19.0'
      details.because 'CVE-2025-48924 fixed in 3.18.0+, use 3.19.0'
    }
  }
}


spotbugs {
    toolVersion = '4.8.6'
    ignoreFailures = false
    effort = 'max'
    reportLevel = 'low'
	excludeFilter = file('config/spotbugs-exclude.xml')
}

tasks.register('spotbugsAll') {
    dependsOn 'spotbugsMain', 'spotbugsTest'
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask).configureEach {
    reports {
        html.required.set(true)
    }
}

dependencyCheck {
    outputDirectory = file("$buildDir/reports/dependency-check")
    formats = ["HTML", "JSON", "SARIF"]
    failBuildOnCVSS = 7.0
    nvd {
        apiKey = System.getenv("NVD_API_KEY")
    }
    analyzers {
        assemblyEnabled = false
    }
}